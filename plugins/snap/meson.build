cargs = ['-DG_LOG_DOMAIN="GsPluginSnap"']

resources_src = gnome.compile_resources(
  'gs-plugin-snap-resources',
  'gs-plugin-snap.gresource.xml',
  source_dir : '.',
  c_name : 'gs_plugin_snap'
)

shared_module(
  'gs_plugin_snap',
  resources_src,
  sources : [
    'gs-plugin-snap.c'
  ],
  include_directories : [
    include_directories('../..'),
    include_directories('../../lib'),
  ],
  install : true,
  install_dir: plugin_dir,
  c_args : cargs,
  dependencies : [ plugin_libs, snap ]
)
metainfo = 'org.gnome.Software.Plugin.Snap.metainfo.xml'

i18n.merge_file(
  input: metainfo + '.in',
  output: metainfo,
  type: 'xml',
  po_dir: join_paths(meson.source_root(), 'po'),
  install: true,
  install_dir: join_paths(get_option('datadir'), 'metainfo')
)

if get_option('tests')
  cargs += ['-DLOCALPLUGINDIR="' + meson.current_build_dir() + '"']
  cargs += ['-DLOCALPLUGINDIR_CORE="' + meson.current_build_dir() + '/../core"']
  e = executable(
    'gs-self-test-snap',
    compiled_schemas,
    sources : [
      'gs-self-test.c'
    ],
    include_directories : [
      include_directories('../..'),
      include_directories('../../lib'),
    ],
    dependencies : [
      plugin_libs,
      snap
    ],
    link_with : [
      libgnomesoftware
    ],
    c_args : cargs,
  )
  test('gs-self-test-snap', e, env: test_env)
endif
